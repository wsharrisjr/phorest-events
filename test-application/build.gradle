buildscript {
    ext {
        gradleDockerComposePluginVersion = '0.4.3'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
        classpath "com.avast.gradle:docker-compose-gradle-plugin:${gradleDockerComposePluginVersion}"
    }
}

apply plugin: 'docker-compose'
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'


sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    provided project.rootProject.project('configuration')
    provided project.rootProject.project('publisher')
    provided project.rootProject.project('model')

    compile "org.codehaus.groovy:groovy"
    compile 'org.springframework.boot:spring-boot-starter-web'

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.spockframework:spock-spring"
    testCompile "org.springframework.amqp:spring-rabbit-test:${springRabbitVersion}"
    testCompile "org.awaitility:awaitility-groovy:3.0.0"
}

jar {
    baseName 'phorest-events-test-application'
}

test {
    reports {
        junitXml.destination = "$buildDir/test-results/unit-test"
        html.destination = "$buildDir/reports/unit-test"
    }
    exclude '**/integration/**'
}

task integrationTests(type: Test) {
    include '**/integration/**'

    reports {
        junitXml.destination = "$buildDir/test-results/integration-test"
        html.destination = "$buildDir/reports/integration-test"
    }
}

integrationTests.doFirst {
    println dockerCompose.servicesInfos.rabbit.rabbit_1.ports[5672]
    systemProperty 'phorest.events.configuration.rabbit.addresses', "localhost:${dockerCompose.servicesInfos.rabbit.rabbit_1.ports[5672]}"
}


dockerCompose.isRequiredBy(integrationTests)

dockerCompose {
    waitForTcpPorts = false
    stopContainers = true
    removeContainers = true
    captureContainersOutput = true
}
